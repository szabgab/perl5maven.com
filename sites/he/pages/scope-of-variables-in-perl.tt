=title טווח (Scope) של משתנים בפרל
=timestamp 2013-07-23T22:00:00
=indexes my, scope
=status show
=books beginner_book
=original scope-of-variables-in-perl
=author szabgab
=translator bruck
=archive 1
=comments 1
=social 1

=abstract start

בפרל ישנם שני סוגי משתנים עיקריים. סוג אחד הוא משתנה שהוא גלובלי בחבילה (package) ומוכרז באחת הצורות הבאות:
<hl>use vars</hl> שהוא מיושן וכבר לא משתמשים בו, או באמצעות <hl>our</hl>.

הסוג השני הוא משתנה לקסיקלי שמוכרז באמצעות  <hl>my</hl>.

נראה מה קורה כשמכריזים על משתנה באמצעות  <hl>my</hl>? באילו חלקים של הקוד ניתן לראות את המשתנה?
במילים אחרות, מה  <b>טווח (scope)</b> של המשתנה)?

=abstract end

<h2>טווח של משתנים: בלוק סוגר</h2>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

{
    my $email = 'foo@bar.com';
    print "$email\n";     # foo@bar.com
}
# print $email;
# $email does not exists
# Global symbol "$email" requires explicit package name at ...
</code>

בתוך הבלוק האנונימי (הסוגריים המסולסלים <hl>{}</hl>), קודם כל אנחנו רואים הכרזה על משתנה חדש בשם
<hl>$email</hl>. המשתנה קיים בין הנקודה שבה הוא הוכרז ועד סוף הבלוק. לכן היה צריך לשים בהערה את השורה שאחרי סגירת הסוגריים המסולסלים
 <hl>}</hl> . אם מסירים את הסימן  <hl>#</hl> מהשורה
<hl># print $email;</hl> , ומנסים להריץ את הקוד, אז מקבלים את שגיאת הקומפילציה הבאה:
<a href="/global-symbol-requires-explicit-package-name">Global symbol "$email" requires explicit package name at ...</a>.

במילים אחרות,  <b>הטווח של כל משתנה שמוכרז עם my הוא הבלוק שבו הוא נמצא אותו.</b>.

<h2>טווח של משתנה: נגיש בכל מקום</h2>

המשתנה<hl>$lname</hl> מוכרז בתחילת הקוד. הוא יהיה נגיש עד סוף הקובץ בכל מקום. אפילו בתוך בלוקים. אפילו אם הבלוקים הם הגדרות של פונקציות.
אם נשנה את המשתנה בתוך הבלוק, אז ערכו ישתנה מאותו מקום ואילך.
אפילו כשעוזבים את הבלוק.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $lname = "Bar";
print "$lname\n";        # Bar

{
    print "$lname\n";    # Bar
    $lname = "Other";
    print "$lname\n";    # Other
}
print "$lname\n";        # Other
</code>


<h2>משתנים שמוסתרים על ידי הכרזות על משתנים אחרים</h2>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $fname = "Foo";
print "$fname\n";        # Foo

{
    print "$fname\n";    # Foo

    my $fname  = "Other";
    print "$fname\n";    # Other
}
print "$fname\n";        # Foo
</code>

במקרה זה מכריזים על המשתנה  <hl>$fname</hl> בתחילת הקוד. כפי שנכתב קודם,הוא יהיה נגיש עד סוף הקובץ, בכל מקום, <b>פרט למקומות שבהם הוא מוסתר על ידי משתנה עם אותו שם שהוגדר מקומיח.</b>.

בתוך הבלוק השתמשנו ב- <hl>my</hl> כדי להכריז על משתנה אחר עם אותו שם. הכרזה זו מסתירה את המשתנה <hl>$fname</hl>
שהוכרז מחוץ לבלוק עד שעוזבים את הבלוק. בסוף הבלוק (בסגירת הסוגריים המסולסלים <hl>}</hl>), המשתנה <hl>$fname</hl>
שהוגדר בתוך הבלוק מושמד והמשתנה המקורי <hl>$fname</hl> שוב יהיה נגיש.
זוהי תכונה חשובה במיוחד כיוון שהיא מאפשרת לנו ליצור משתנים בטווחים קטנים בלי שנצטרך לחשוב על שימושים אפשריים של שמותיהם מחוץ לאותם טווחים.

<h2>חזרה על אותו שם בבלוקים שונים</h2>

אתם חופשיים להשתמש באותו שם משתנה במספר בלוקים שונים. למשתנים האילו אין שום קשר זה לזה.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

{
    my $name  = "Foo";
    print "$name\n";    # Foo
}
{
    my $name  = "Other";
    print "$name\n";    # Other
}
</code>

<h2>הכרזות על חבילה (package) בתוך קובץ</h2>


הדוגמה הבאה היא קצת יותר מתקדמת, אבל נראה לי שחשוב להזכיר אותה כאן.

פרל מאפשרת לנו להחליף בין  <b>מרחבי שמות (name-spaces)</b> באצעות מילת המפתח <hl>package</hl>  בתוך קובץ. הכרזה על חבילה  <b>לא</b> מייצרת טווח. אם תכריזו על משתנה בחבילה <b>main </b> שהיא פשוט הקוד שלך, אז המשתנה  <hl>$fname</hl> יהיה נגיש גם במרחבי שמות אחרים באותו קובץ.


אם תכריזו על משתנה בשם  <hl>$lname</hl> במרחב השמות Other
הוא יהיה נגיש כשתחזרו מאוחר יותר למרחב השמות <hl>main</hl> . אם ההכרזה על החבילה <hl>package Other</hl> הייתה בקובץ אחר
אז למשתנים יהיו טווחים שונים כאשר הטווח של כל משתנה נוצר על ידי הקובץ שבו הוא נמצא.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $fname  = "Foo";
print "$fname\n";    # Foo

package Other;
use strict;
use warnings;

print "$fname\n";    # Foo
my $lname = 'Bar';
print "$lname\n";    # Bar


package main;

print "$fname\n";    # Foo
print "$lname\n";    # Bar
</code>


