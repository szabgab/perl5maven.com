=title Using a queue in Perl
=timestamp 2013-05-19T20:30:01
=indexes push, shift, queue, array, last, FIFO
=status show
=books beginner_book
=author szabgab
=index 1
=archive 1
=feed 1
=comments 1
=social 1

=abstract start

There are various applications where processing a queue is useful.

For example you need to process a deep directory structure where each
directory can contain subdirectories.

Maybe you manage a build systems where each unit has a list
of prerequisites and you need to traverse the whole dependency tree.

If I want to give a less painful example, you write an interactive application
that will handle people who need treatment at the dentist.

In either of these cases a queue can work really nicely.

=abstract end

<h2>What is a queue?</h2>

A queue is basically a list of items. When new items arrive they are added to the end of the list.
When the queue "goes forward", the first item in the list is removed and all the other items "move forward".

There are various standard data structures described in computer sciences. The abstract description
of a queue is <a href="http://en.wikipedia.org/wiki/FIFO">FIFO - First In First Out</a>.
The first who arrived in to the queue will be the first to leave it.

In Perl, a regular array using the <hl>push</hl> and <hl>shift</hl> functions can be used to implement a queue.
If you need a reminder about those functions check out the <a href="/manipulating-perl-arrays">article about push and shift</a>.

Let's see an example:

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my @people = ("Foo", "Bar");
while (@people) {
    my $next_person = shift @people;
    print "$next_person\n"; # do something with this person

    print "Type in the names of more people:";
    while (my $new = <STDIN>) {
        chomp $new;
        if ($new eq "") {
            last;
        }
        push @people, $new;
    }
    print "\n";
}
</code>

Here we use the <hl>@people</hl> array to hold the queue. It starts out with two elements.
Then the whole queue processing is in the <a href="/while-loop">while loop</a>.
That loop will run as long as there are names in the queue.

(In case you wonder, in <a href="/scalar-and-list-context-in-perl">scalar context</a> as is the
condition of the <hl>while ()</hl> loop, the array returns its size, the number of elements it holds.
If the array is empty this will be 0 which is <a href="/boolean-values-in-perl">false</a>.
If there are elements in the array the size is a positive integer which is 
<a href="/boolean-values-in-perl">true</a> in Perl.)

As the first thing inside the loop, we fetch the first element from the queue using the
<hl>shift</hl> function. This is the next person to be treated by the dentist.
The next thing we should call is <hl>treat($next_person);</hl> but for now we just print his name.

Once the treatment is over we can check if there are more people waiting to be added to the queue.
We implemented this in another <hl>while</hl> loop. We wait for the user to type in names one-by-one.
We remove the trailing newline, and then check if the input was empty or not. If it was empty,
meaning the user pressed ENTER without typing in any name, we call <hl>last</hl>.
That will terminate the internal while loop and reach the <hl>print "\n";</hl> line.
Then perl will go back to the beginning of the main <hl>while-loop</hl> treating the next person.
If the user typed in a name, that name will be <b>pushed</b> to the end of the <hl>@people</hl> array,
to the end of the queue. Then, still in the internal loop, we wait for another name to be typed in.

As long as there are more people arriving than we can treat, the queue will grow, but if for a while
no new people arrive, the process will slowly deal with all the people in the queue.
At one point the array will become empty and the main while-loop will finish.

<h2>Abstract implementation</h2>

The following code is a more abstract version of the same code using
<a href="/subroutines-and-functions-in-perl">functions</a>
that we have not implemented yet.

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my @queue = accept_new_to_queue();
while (@queue) {
    my $next_item = shift @queue;
	handle_item($next_item);

	push @queue, accept_new_to_queue()
}

sub accept_new_to_queue {
    ...
}
sub handle_item {
    ...
}
</code>


<h2>Parallel or Asynchronous processing</h2>

The above implementation can work in simple cases, but it has a major problem.
Both the <hl>accept_new_to_queue()</hl> and the <hl>handle_item()</hl> are blocking.
That is, while you are handling an item, no other items can be added to the queue.
This can be acceptable when processing a directory tree or a list of dependencies,
but people would be upset at the dentist if they could not even enter the waiting room
while one of the patients is receiving treatment.

For this you need some way of parallel or asynchronous processing so the
<hl>accept_new_to_queue()</hl> and the <hl>handle_item</hl> can be run <b>"virtually at the same time"</b>.
I wrote virtually as they don't necessarily need to run in parallel. We just need to perceive as if they did.

If you have multiple CPUs or at least multiple cores, which is the case in all modern computer,
you could theoretically run parts of the code really at the same time. The two major solutions
for this is <b>threading</b> and <b>forking</b>. While in many languages threading
is the preferred way, in Perl most people will use forking.
If you go that way, you should probably take a look at the 
<a href="https://metacpan.org/module/Parallel::ForkManager">Parallel::ForkManager</a>
module on CPAN.

For asynchronous or even-driven perl programming, you might want to take a look
at <a href="https://metacpan.org/module/POE">POE</a>.

We will see examples for both cases in other articles.


