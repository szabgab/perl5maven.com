=title Documenti Here, ovvero come creare stringhe multi-linea in Perl
=timestamp 2013-06-30T08:01:01
=indexes <<, /m, /g, q, qq
=status show
=original here-documents
=books beginner_book
=author szabgab
=translator giatorta
=archive 1
=comments 1
=social 1

=abstract start

Once in a while you need to create a string that spreads on several lines.
As usual in Perl, there are several solutions for this.
Using a here-document is one of the common solutions.

=abstract end

A <b>here-document</b> allows you to create a string that spreads on <b>multiple lines</b> and preserves
white spaces and new-lines. If you run the following code it will print exactly what you see
starting from the word Dear till the line before the second appearance of END_MESSAGE.

<h2>Non-interpolating here document</h2>

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $name = 'Foo';

my $message = <<'END_MESSAGE';
Dear $name,

this is a message I plan to send to you.

regards
  the Perl Maven
END_MESSAGE

print $message;
</code>

Output:

<code>
Dear $name,

this is a message I plan to send to you.

regards
  the Perl Maven
</code>

The here document starts with two less-than characters <hl>&lt;&lt;</hl> followed by and arbitrary string that becomes
the designated end-mark of the here-document, followed by the semi-colon <hl>;</hl> marking the end of the statement.
This is a bit strange as the statement does not really end here. Actually the content of the here document
just starts on the line after the semi-colon, (in our case with the word "Dear"), and continuous till perl finds the
arbitrarily selected end-mark. In our case the string <b>END_MESSAGE</b>.

If you have already seen here-documents in code, you are probably surprised by the single-quotes around the first
<b>END_MESSAGE</b>. I think if you find examples of here-documents on the Internet, or behind the corporate firewalls,
you'll probably see the opening part without any quotes around it. Like this:

<code lang="perl">
my $message = <<END_MESSAGE;
...
END_MESSAGE
</code>

It works and it behaves the same as if you put double-quotes around the END_MESSAGE like in the next example,
but it is <b>deprecated</b> and will be removed from 5.20. Do <b>not</b> use it! Do not use here-document
without quotes around the definition of the end-string.

<code lang="perl">
my $message = <<"END_MESSAGE";
...
END_MESSAGE
</code>

If you already know the
<a href="/quoted-interpolated-and-escaped-strings-in-perl">difference between single-quotes and double-quotes</a>
in Perl, then you won't be surprised that the here-documents have the exact same behavior.
The only difference is, that the quotes are
around the end-mark and not the actual string. If no quotes are provided, Perl defaults to double-quotes.

If you look back to the first example, you will notice we had <hl>$name</hl> part of the here-document
and it also remained part of the output. That's because Perl did not try to fill the place
with the content of the <hl>$name</hl> variable. (We did not even have to declare that variable in the
code. You can try the script, even without the <hl>my $name = 'Foo';</hl> part. 

<h2>Interpolating here document</h2>

In the next example we use double quotes around the end-mark and thus it will interpolate
the <hl>$name</hl> variable:

<code lang="perl">
use strict;
use warnings;

my $name = 'Foo';
my $message = <<"END_MSG";
Hello $name,

how are you?
END_MSG

print $message;
</code>

The result of running this script is:

<code>
Hello Foo,

how are you?
</code>

<h2>Warning: exact end-string at the end</h2>

Maybe just a note. You have to make sure the end-string is duplicated at the end of the string
<b>exactly</b> as it was at the beginning. No white-spaces before, and no white spaces after.
Otherwise Perl will not recognize it and will think the here-documents have not ended yet.
That means you cannot indent the end tag to match the indentation of the rest of your code.
Or can you?

<h2>Here documents and code indentation</h2>

If the here document needs to be in a place where we'd normally indent
code, we have two problems:


<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $name = 'Foo';
my $send = 1;

if ($send) {
    my $message = <<"END_MESSAGE";
        Dear $name,
    
        this is a message I plan to send to you.
    
        regards
          the Perl Maven
END_MESSAGE
    print $message;
}
</code>

One is that as I mentioned above, the end string must be exactly the same both at its
declaration and at the end of the string, so you cannot indent it at the end. 

The other problem is that the output will have all those leading whites-spaces in every line:

<code>
        Dear Foo,
    
        this is a message I plan to send to you.
    
        regards
          the Perl Maven
</code>

The lack of indentation of the end-mark can be solved by using an one that already
includes enough leading white-spaces: (I am using 4 real spaces here, as the tabs don't
work well in the article, but they could work in real code. If you are in the indent-by-tab
camp.)

<code lang="perl">
    my $message = <<"    END_MESSAGE";
       ...
    END_MESSAGE
</code>

The extra indentation of the actual text can be removed using a substitution at the assignment.

<code lang="perl">
    (my $message = <<"    END_MESSAGE") =~ s/^ {8}//gm; 
        ...
    END_MESSAGE
</code>

In the substitution we replace 8 leading spaced by the empty string. We use two modifier:
<hl>/m</hl> changes the behavior of <hl>^</hl> from matching at the <b>beginning of the string</b>
to match at the <b>beginning of line</b>.  <hl>/g</hl> tells the substitution to work <b>globally</b>,
that is to repeat the substitution as many times as it can.

Together these two flag will get the substitution to remove 8 leading spaces from every line in the
variable on the left-hand side of <hl>=~</hl>.
On the left-hand side we had to put the assignment in parentheses because the precedence of the
assignment operator (<hl>=</hl>) is lower than the precedence of the regex <hl>=~</hl>. Without
the parentheses, perl would first try to run the regex substitution on the here-document itself
resulting in a compile-time error:

Can't modify scalar in substitution (s///) at programming.pl line 9, near "s/^ {8}//gm;"

<h2>Using q or qq instead</h2>

With all this explanation, actually I am not sure if I should really recommend using here-documents.
In many cases, instead of here-documents, I use either the <hl>qq</hl> or the <hl>q</hl> operator.
Depending if I want interpolating, or non-interpolating strings:

<code lang="perl">
#!/usr/bin/perl
use strict;
use warnings;

my $name = 'Foo';
my $send = 1;

if ($send) {
    (my $message = qq{
        Dear $name,
    
        this is a message I plan to send to you.
    
        regards
          the Perl Maven
        }) =~ s/^ {8}//mg;
    print $message;
}
</code>

